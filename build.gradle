// kotlin and bintary
buildscript {
    ext.kotlin_version = '1.1.2-2' // for kotlin
    repositories {
        mavenCentral() // for kotlin
        jcenter() // for bintary
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" // for kotlin
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3" // for bintary
    }
}
apply plugin: 'kotlin'
apply plugin: "com.jfrog.bintray"

apply plugin: 'application'
apply plugin: 'jacoco'
apply plugin: "maven-publish"

group "com.keidrun.qstackoverflow"
//version = "0.0.1"  //gradle -Pversion=0.0.1 bintrayUpload
def tag = "v${project.version}"
def productName = 'Stack Overflow\'s Search CLI'
description = "${project.name} build of the version ${project.version}"

def appName = 'sos'
mainClassName = 'com.keidrun.qstackoverflow.app.AppKt'
defaultTasks 'run'
startScripts {
    applicationName = appName
}
distZip {
    baseName = appName
    // skip parent directory
    eachFile { file ->
        String path = file.relativePath
        file.setPath(path.substring(path.indexOf("/")+1,path.length()))
    }
}

def defaultEncoding = 'UTF-8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = defaultEncoding
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.github.kevinsawicki:http-request:6.0'
    compile 'commons-cli:commons-cli:1.4'
    compile 'commons-codec:commons-codec:1.10'
    compile 'org.json:json:20160810'
    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile 'com.github.stefanbirkner:system-rules:1.16.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.5"
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': mainClassName
        )
    }
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}
task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}
jar.dependsOn sourceJar, javadocJar

// coverage
jacoco {
    toolVersion = "0.7.+"
}

task codeCoverageReport(type: JacocoReport, dependsOn: 'test') {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    sourceSets sourceSets.main

    reports {
        xml.enabled = true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled = false
        csv.enabled = false
    }
}

// publish
publishing {
    publications {
        mod(MavenPublication) {
            from components.java

            artifact sourceJar
            artifact javadocJar

            pom.withXml { xml ->
                final thisPom = {
                    packaging "jar"
                    name "$productName"
                    url "https://github.com/keidrun/qstackoverflow"
                    description "${project.description}"
                    licenses {
                        license {
                            name "MIT License"
                            url "https://opensource.org/licenses/MIT"
                            distribution "repo"
                        }
                    }
                    scm {
                        url "https://github.com/keidrun/qstackoverflow"
                        connection "https://github.com/keidrun/qstackoverflow"
                    }
                    developers {
                        developer {
                            id "keidrun"
                            name "Keid"
                            email "kssk96@gmail.com"
                        }
                    }
                }
                thisPom.resolveStrategy = Closure.DELEGATE_FIRST
                xml.asNode().children().last() + thisPom
            }
        }
    }

    repositories {
        maven {
            name "local"
            url "$buildDir/repositories/repo"
        }
    }
}

// upload by bintray
bintray {
    user = project.hasProperty("bintrayUser") ? bintrayUser : ""
    key = project.hasProperty("bintrayKey") ? bintrayKey : ""
    publications = ["mod"]
    publish = true
    pkg {
        repo = "qstackoverflow"
        name = "qstackoverflow"
        websiteUrl = "https://github.com/keidrun/qstackoverflow"
        issueTrackerUrl = "https://github.com/keidrun/qstackoverflow/issues"
        vcsUrl = "https://github.com/keidrun/qstackoverflow.git"
        licenses = ["MIT"]
        labels = ["stackoverflow", "stackexchange"]
        publicDownloadNumbers = true

        version {
            name = tag
            desc = "Release version ${project.version}"
            released = new Date()
            vcsTag = tag
            gpg {
                sign = true
            }
        }
    }
}
